import com.expediagroup.graphql.plugin.gradle.config.GraphQLSerializer
import com.expediagroup.graphql.plugin.gradle.config.GraphQLScalar

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'com.expediagroup.graphql'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'

group = 'com.dashx'
version = '1.0.13'

android {
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33
        versionCode 5
        versionName version

        buildConfigField 'String', 'VERSION_NAME', "\"$version\""
        buildConfigField 'String', 'LIBRARY_NAME', "\"dashx-android\""
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
    }
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.11'

    // Kotlin Graphql
    implementation 'org.slf4j:slf4j-simple:2.0.7'
    implementation "com.expediagroup:graphql-kotlin-ktor-client:$latestGraphQLKotlinVersion"
    implementation "io.ktor:ktor-client-okhttp:$ktorVersion"
    implementation "io.ktor:ktor-client-logging-jvm:$ktorVersion"
    implementation 'androidx.work:work-runtime-ktx:2.7.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    implementation platform('com.google.firebase:firebase-bom:31.5.0')
    implementation 'com.google.firebase:firebase-messaging-ktx:23.1.2'

    implementation 'com.google.android.gms:play-services-ads-identifier:18.0.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

graphql {
    client {
        endpoint = 'https://api.dashx-staging.com/graphql'
        packageName = 'com.dashx.graphql.generated'

        customScalars = [new GraphQLScalar('JSON', 'kotlinx.serialization.json.JsonObject', 'com.dashx.graphql.scalars.converters.JsonScalarConverter')]
        serializer = GraphQLSerializer.KOTLINX
    }
}

android.sourceSets {
    main.kotlin.srcDirs 'build/generated/source/graphql/main'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"
