apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {
    PUBLISH_GROUP_ID = 'com.dashx'
    PUBLISH_VERSION = '1.0.0'
    PUBLISH_ARTIFACT_ID = 'dashx-android'
    PUBLISH_DESCRIPTION = 'DashX Android SDK'
    PUBLISH_URL = 'https://github.com/dashxhq/dashx-android/'
    PUBLISH_LICENSE_NAME = 'MIT License'
    PUBLISH_LICENSE_URL = 'https://github.com/dashxhq/dashx-android/blob/master/LICENSE'
    PUBLISH_SCM_CONNECTION = 'scm:git:github.com/dashxhq/dashx-android.git'
    PUBLISH_SCM_DEVELOPER_CONNECTION = 'scm:git:ssh://github.com/dashxhq/dashx-android.git'
    PUBLISH_SCM_URL = 'https://github.com/dashxhq/dashx-android/tree/master'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                }

                artifact androidSourcesJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = PUBLISH_DESCRIPTION
                    url = PUBLISH_URL
                    licenses {
                        license {
                            name = PUBLISH_LICENSE_NAME
                            url = PUBLISH_LICENSE_URL
                        }
                    }
                    developers {
                        developer {
                            id = 'pradeep.dashx'
                            name = 'Pradeep Kumar'
                            email = 'pradeep@dashx.com'
                        }

                        developer {
                            id = 'puli.ajay'
                            name = 'Puli Ajay'
                            email = 'puliajay19@gmail.com'
                        }

                        developer {
                            id = 'sravan.kumar'
                            name = 'Sravan Kumar'
                            email = 'sravankaraka.149@gmail.com'
                        }
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        connection = PUBLISH_SCM_CONNECTION
                        developerConnection = PUBLISH_SCM_DEVELOPER_CONNECTION
                        url = PUBLISH_SCM_URL
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
        rootProject.ext["signing.keyId"],
        rootProject.ext["signing.key"],
        rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
